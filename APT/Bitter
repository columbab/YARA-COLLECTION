import "pe"

rule APT_Bitter_Almond_RAT {

meta:
description = "Detects Bitter (T-APT-17) Almond RAT (.NET)"
author = "SECUINFRA Falcon Team (@SI_FalconTeam)"
tlp = "WHITE" reference = " https://www.secuinfra.com/en/techtalk/whatever-floats-your-boat-bitter-apt-continues-to-target-bangladesh"
date = "2022-06-01" hash = "55901c2d5489d6ac5a0671971d29a31f4cdfa2e03d56e18c1585d78547a26396"

strings:
$function0 = "GetMacid" ascii
$function1 = "StartCommWithServer" ascii
$function2 = "sendingSysInfo" ascii
$dbg0 = "*|END|*" wide
$dbg1 = "FILE>" wide
$dbg2 = "[Command Executed Successfully]" wide

condition:
uint16(0) == 0x5a4d
and filesize > 12KB // Size on Disk/1.5
and filesize < 68KB // Size of Image*1.5
and any of ($function*)
and any of ($dbg*)
}
rule APT_Bitter_Downloader_PE_01 : Bitter Downloader
{
    meta:
        author = "HONKONE"
        description = "Bitter Downloader"

    strings:
        $PE_header = "!This program cannot be run in DOS mode." wide ascii
        $func1 = "ExitProcess" wide ascii
        $func2 = "GetProcess" wide ascii
        $func3 = "FreeEnvironmentStringsW" wide ascii
        $func4 = "GetTickCount" wide ascii
        $func5 = "WriteConsoleW" wide ascii
        $func6 = "LoadLibraryA" wide ascii
        $load_dll_1 = "mscoree.dll" wide ascii
        $load_dll_2 = "KERNEL32.DLL" wide ascii
        $load_dll_3 = "USER32.DLL" wide ascii
        $load_dll_4 = "WS2_32.dll" wide ascii
        $pdb1 = "C:\\Users\\UserA\\Documents\\Visual Studio 2008\\Projects\\Artra\\Release\\Artra.pdb" wide nocase ascii
    	$pdb2 = "C:\\Users\\ULTRON\\Documents\\Visual Studio 2008\\Projects\\Down02Sept\\Release\\Down02Sept.pdb" wide nocase ascii
    	$pdb3 = "C:\\Users\\INFINITE\\Documents\\Visual Studio 2008\\Projects\\NewDown\\Release\\NewDown.pdb" wide nocase ascii
    	$pdb4 = "C:\\Users\\UserA\\Documents\\Visual Studio 2008\\Projects\\Artra\\Release\\Artra.pdb" wide nocase ascii
    	$pdb5 = "C:\\poke\\Release\\poke.pdb" wide nocase ascii
    	$pdb6 = "C:\\Users\\ARAGON\\Documents\\Visual Studio 2008\\Projects\\DownWin32\\Release\\DownWin32.pdb" wide nocase ascii
    	$pdb7 = "C:\\Users\\INFINITE\\Documents\\Visual Studio 2008\\Projects\\DownWin32\\Release\\DownWin32.pdb" wide nocase ascii
    	$pdb8 = "C:\\Users\\ULTRON\\Documents\\Visual Studio 2008\\Projects\\Down02Sept\\Release\\Down02Sept.pdb" wide nocase ascii
    	$pdb9 = "C:\\Users\\ANONYMOUS\\Documents\\Visual Studio 2008\\Projects\\Down Free\\DownWin32\\Release\\DownWin32.pdb" wide nocase ascii
    	$pdb10 = "C:\\medal\\Release\\medal.pdb" wide nocase ascii
    	$pdb11 = "C:\\iexpo\\Release\\iexpo.pdb" wide nocase ascii
    	$pdb12 = "E:\\RATFUD\\dllhost\\Release\\dllhost.pdb" wide nocase ascii
    	$pdb13 = "C:\\Users\\pc5\\Documents\\Visual Studio 2008\\Projects\\WMIS\\Release\\WMIS.pdb" wide nocase ascii
    	$pdb14 = "D:\\MyWork\\VisualSudio\\mwow\\Debug\\mwow.pdb" wide nocase ascii
    	$pdb15 = "c:\\Users\\Dexter\\Documents\\Visual Studio 2008\\Projects\\1\\Release\\1_3.pdb" wide nocase ascii
    condition:
        $PE_header and 3 of ($func*) and all of ($load_dll*) and 1 of ($pdb*)
}

rule APT_Bitter_Jan4: Bitter RTF Downloader
{
    meta:
        author = "HONKONE"
        description = "new rtf downloader for bitter"
        date = "2019-01-04"
    strings:
        $rtf_hedaer = "{\\rt"
        $str = /6A6B6C613A2F2F[\d\w]+/ ascii nocase
        $func1 = "4372656174654469726563746F727941" nocase ascii
        $func2 = "4C6F61644C69627261727941" nocase ascii
        $func3 = "75726C6D6F6E2E646C6C" nocase ascii
        $func4 = "6F776E6C6F6164546F46696C6541" nocase ascii
        $func5 = "4D6F766546696C6541" nocase ascii
        $func6 = "5368656C6C33322E646C6C" nocase ascii
        $func7 = "5368656C6C4578656375746541" nocase ascii
        $func8 = "6F70656E" nocase ascii
    condition:
        $str and $rtf_hedaer and all of ($func*)
}


rule APT_Bitter_Maldoc_Verify {

meta:
description = "Detects Bitter (T-APT-17) shellcode in oleObject (CVE-2018-0798)"
author = "SECUINFRA Falcon Team (@SI_FalconTeam)"
tlp = "WHITE"
reference = "https://www.secuinfra.com/en/techtalk/whatever-floats-your-boat-bitter-apt-continues-to-target-bangladesh"
date = "2022-06-01"
hash0 = "0c7158f9fc2093caf5ea1e34d8b8fffce0780ffd25191fac9c9b52c3208bc450"
hash1 = "bd0d25194634b2c74188cfa3be6668590e564e6fe26a6fe3335f95cbc943ce1d"
hash2 = "3992d5a725126952f61b27d43bd4e03afa5fa4a694dca7cf8bbf555448795cd6"

strings:
// This rule is meant to be used for verification of a Bitter Maldoc
// rather than a hunting rule since the oleObject it is matching is
// compressed in the doc zip

$xor_string0 = "LoadLibraryA" xor
$xor_string1 = "urlmon.dll" xor
$xor_string2 = "Shell32.dll" xor
$xor_string3 = "ShellExecuteA" xor
$xor_string4 = "MoveFileA" xor
$xor_string5 = "CreateDirectoryA" xor
$xor_string6 = "C:\\Windows\\explorer" xor
$padding = {000001128341000001128341000001128342000001128342}

condition:
3 of ($xor_string*)
and $padding
}
rule APT_Bitter_PDB_Paths {

meta:
description = "Detects Bitter (T-APT-17) PDB Paths"
author = "SECUINFRA Falcon Team (@SI_FalconTeam)"
tlp = "WHITE"
reference = "https://www.secuinfra.com/en/techtalk/whatever-floats-your-boat-bitter-apt-continues-to-target-bangladesh"
date = "2022-06-22"
hash0 = "55901c2d5489d6ac5a0671971d29a31f4cdfa2e03d56e18c1585d78547a26396"

strings:
// Almond RAT
$pdbPath0 = "C:\\Users\\Window 10 C\\Desktop\\COMPLETED WORK\\" ascii
$pdbPath1 = "stdrcl\\stdrcl\\obj\\Release\\stdrcl.pdb"

// found by Qi Anxin Threat Intellingence Center
// reference: https://mp.weixin.qq.com/s/8j_rHA7gdMxY1_X8alj8Zg
$pdbPath2 = "g:\\Projects\\cn_stinker_34318\\"
$pdbPath3 = "renewedstink\\renewedstink\\obj\\Release\\stimulies.pdb"

condition:
uint16(0) == 0x5a4d
and any of ($pdbPath*)
}
rule APT_Bitter_RTF_01:RTF OR DOCX
{
	meta:
		author = "HONKONE"
		md51 = "0F1E75A70FC90ADCA17710F7D65F205B"
		md52 = "3D163CC7329ED049D3E32B5D11586236"
		md53 = "4E09AA10E8F0C40E74733A9345695D84"
		md54 = "981DEB00AAC4F6EBE9ACEB401DEDE6F3"
		md55 = "68907B9AB744C9D9CAE4C27E0B82EB4A"
		md56 = "E152B5B7E9079F689EBAAA9B8FE2ED66"
		md57 = "CEE5AF57AC60B023F2C715388E34F6DB"
		md58 = "E39629316B4BF049FC9704DD00E41B6A"
		md59 = "B5D73517BB02545E8776DB4C8A66B62A"
	strings:
		$domain1 = "62617A61726172656E612E636F6D" nocase
		$domain2 = "7363686F6C61727339302E77656273697465" nocase
		$domain3 = "7A6D77617264726F62652E636F6D" nocase
		$domain4 = "686172747261646572732E636F6D" nocase
		$domain5 = "7363686F6C61727339302E77656273697465" nocase
		$domain6 = /63[\n\r]{0,}7265656439302E636F6D/ nocase
		/*
		creed90.com
		scholars90.website
		hartraders.com
		scholars90.website
		http://bazararena.com/mlr
		zmwardrobe.com
		*/
		$func1 = "4D6F766546696C6541" nocase
		$func2 = "6F70656E" nocase
		$func3 = "5368656C6C4578656375746541" nocase
		$func4 = /55524C446F776E6C6F6164546F46[\n\r]{0,}696C6541/ nocase
		$func5 = "57696E457865" nocase
		$func6 = "57696E45786563" nocase
		$func7 = "6D6F7665" nocase
		/*
		$pe_name1 = "637376742E657865" nocase
		$pe_name2 = "696766782E657865" nocase
		$pe_name3 = "636E682E657865" nocase
		$pe_name4 = "637376782E657865" nocase
		$pe_name5 = "73706C7372762E657865" nocase
		"636F6E686F73742E657865"
		*/
	condition:
		1 of ($domain*) and (2 of ($func*))
}

rule EXT_APT_Bitter_Win32k_0day_Feb21 {
   meta:
      description = "Detects code that exploits a Windows 0day exploited by Bitter APT group"
      author = "dbappsecurity_lieying_lab"
      date = "2021-01-01"
      reference = "https://ti.dbappsecurity.com.cn/blog/index.php/2021/02/10/windows-kernel-zero-day-exploit-is-used-by-bitter-apt-in-targeted-attack/"
   strings:
      $s1 = "NtUserConsoleControl" ascii wide
      $s2 = "NtCallbackReturn" ascii wide
      $s3 = "CreateWindowEx" ascii wide
      $s4 = "SetWindowLong" ascii wide
      $a1 = {48 C1 E8 02 48 C1 E9 02 C7 04 8A}
      $a2 = {66 0F 1F 44 00 00 80 3C 01 E8 74 22 FF C2 48 FF C1}
      $a3 = {48 63 05 CC 69 05 00 8B 0D C2 69 05 00 48 C1 E0 20 48 03 C1}
   condition:
      uint16(0) == 0x5a4d and all of ($s*) and 1 of ($a*)
}
rule APT_Bitter_ZxxZ_Downloader {

meta:
description = "Detects Bitter (T-APT-17) ZxxZ Downloader"
author = "SECUINFRA Falcon Team (@SI_FalconTeam)"
tlp = "WHITE"
reference = " https://www.secuinfra.com/en/techtalk/whatever-floats-your-boat-bitter-apt-continues-to-target-bangladesh"
date = "2022-06-01"
update= "2023-03-16"
hash0 = "91ddbe011f1129c186849cd4c84cf7848f20f74bf512362b3283d1ad93be3e42"
hash1 = "90fd32f8f7b494331ab1429712b1735c3d864c8c8a2461a5ab67b05023821787"
hash2 = "69b397400043ec7036e23c225d8d562fdcd3be887f0d076b93f6fcaae8f3dd61"
hash3 = "3fdf291e39e93305ebc9df19ba480ebd60845053b0b606a620bf482d0f09f4d3"
hash4 = "fa0ed2faa3da831976fee90860ac39d50484b20bee692ce7f0ec35a15670fa92"

strings:
// old ZxxZ samples / decrypted strings
$old0 = "MsMp" ascii
$old1 = "SOFTWARE\\Microsoft\\Windows NT\\CurrentVersion" ascii
$old2 = "&&user=" ascii
$old3 = "DN-S" ascii //c2 command for download success
$old4 = "RN_E" ascii //c2 command for download fail

/*
strings will match stealthworker samples
// new ZxxZ samples
$c2comm0 = "GET /" ascii
$c2comm1 = "profile" ascii
$c2comm2 = ".php?" ascii
$c2comm3 = "data=" ascii
$c2comm4 = "Update" ascii
$c2comm5 = "TTT" ascii
*/

// new ZxxZ samples 20230314
$c2comm0 = "GET /" ascii
$c2comm1 = "profiles" ascii
$c2comm2 = ".php?" ascii
$c2comm3 = "data=" ascii
$c2comm4 = "Update" ascii
$c2comm5 = "TTT" ascii
$c2comm6 = "error" ascii

/*
.text:0040245A 8A 1C 29                                      mov     bl, [ecx+ebp]
.text:0040245D 30 1C 38                                      xor     [eax+edi], bl
.text:00402460 40                                            inc     eax
*/
//xor decrypt
$xor0={ 8a [5] 30 [6] 4? }
$xor1={ 8a [2] 30 [2] 4? }

//condition will match lots of dex
/*
condition:
uint16(0) == 0x5a4d
and filesize > 39KB // Size on Disk/1.5
and filesize < 2MB // Size of Image*1.5

and (all of ($old*)) or (all of ($c2comm*))
*/

condition:
uint16(0) == 0x5a4d
and filesize > 39KB // Size on Disk/1.5
and filesize < 2MB // Size of Image*1.5

and ((all of ($old*)) or ( (all of ($c2comm*)) and (1 of ($xor*))))

}
