rule APT_Reaper_Chinotto {
    meta:
        id = "eff8fd11-dc7a-4011-b083-181d0cca8790"
        version = "1.0"
        malware = "Chinotto"
        intrusion_set = "Reaper"
        description = "Detects obfuscation or string of Chinotto"
        source = "SEKOIA.IO"
        creation_date = "2023-02-27"
        classification = "TLP:WHITE"
    strings:
        $chunk_1 = {
            C7 85 ?? ?? ?? ?? ?? ?? ?? 00
            C7 85 ?? ?? ?? ?? ?? ?? ?? 00
            33 C0
            EB 03
            8D 49 00
            8B 8C 85 ?? ?? ?? ??
            3B 8C 85 ?? ?? ?? ??
        }
        $chunk_2 = {
            C7 84 24 ?? ?? ?? ?? ?? ?? 0? 00
            C7 84 24 ?? ?? ?? ?? ?? ?? 0? 00
            33 C0
            EB 0D
            8D A4 24 00 00 00 00
            8D 9B 00 00 00 00
            8B 8C 84 ?? ?? ?? ??
            3B 8C 84 ?? ?? ?? ??
        }
        $movs_zip_dir_start = { C7 45 ?? 5A 69 
            70 20 C7 45 ?? 44 69 72 20 C7 45 
            ?? 53 74 61 72  C7 45 ?? 74 20 
            2D 20
        }
    condition:
        uint16be(0) == 0x4d5a and
        filesize < 1MB and 
        ($chunk_1 or $chunk_2 or $movs_zip_dir_start)
 }
rule apt_Reaper_Malicious_HTA_file {
    meta:
        id = "22a98c27-8ff4-4760-b505-f8eacf4dabda"
        version = "1.0"
        intrusion_set = "Reaper"
        description = "Detects malicious Reaper HTA files"
        source = "SEKOIA.IO"
        creation_date = "2023-03-06"
        classification = "TLP:WHITE"
    strings:
        $s1 = "<HTML>" nocase
        $s2 = " UwB0AGEAcgB0AC0AUwBs" ascii
        $s3 = "= new ActiveXObject(" ascii
        $s4 = "\", \"\", \"open\", 0);" ascii
        $s5 = ".moveTo(" ascii
        $s6 = "self.close();"
    condition:
        $s1 at 0 and all of them and filesize < 1MB
}
rule apt_Reaper_Chinotto_PowerShell_Variant {
    meta:
        id = "fa42b225-58fe-4e00-b84b-df37491d8fdd"
        version = "1.0"
        malware = "Chinotto"
        intrusion_set = "Reaper"
        description = "Detects Reaper Chinotto Powershell Variant"
        source = "SEKOIA.IO"
        creation_date = "2023-03-06"
        classification = "TLP:WHITE"
    strings:
    	$ = "$env:COMPUTERNAME + '-' + $env:USERNAME;" ascii wide
        $ = "while($true -eq $true)" ascii wide
        $ = "Start-Sleep -Seconds" ascii wide
        $ = " -ne 'null' -and $" ascii wide
        $ = "= 'R=' + [System.Convert]::" ascii wide
        $ = "[string]$([char]0x0D) + [string]$([char]0x0A);" ascii wide
    condition:
        4 of them
}
rule apt_Reaper_2FA_Phishing_webpage {
    meta:
        id = "348ca2ad-c8f9-4aed-8a27-95caa3a34f4b"
        version = "1.0"
        intrusion_set = "Reaper"
        description = "Detects Reaper 2FA phishing webpage"
        source = "SEKOIA.IO"
        creation_date = "2023-03-09"
        classification = "TLP:WHITE"
    strings:
        $ = "setTimeout(checkUpload,"
        $ = "commChannel.addListener("
        $ = "else if(commType =="
        $ = "?dir=DOWN&method=READ&id="
        $ = "Content : base64_encode(upload_data)"
        $ = "$.post(upHttpRelayer"
        $ = "var ablyUpData = {"
        $ = "initComm();"
        $ = "function Next(arg) {"
    condition:
        3 of them
}
rule apt_Reaper_AblyGo_Reverse_Shell {
    meta:
        id = "77778ef6-5d24-4888-93c8-390066dbf361"
        version = "1.0"
        source = "SEKOIA.IO"
        intrusion_set = "Reaper"
        description = "Detects AblyGo reverse shell implant"
        creation_date = "2023-03-09"
        classification = "TLP:WHITE"
    strings:
        $ = "main.reverse.func1" 
        $ = "github.com/ably/ably-go/ably.(*RealtimeChannels).Get" 
        $ = "github.com/ably/ably-go/ably.WithKey"
    condition:
        (uint32be(0) == 0x7f454c46 or uint16be(0) == 0x4d5a) and
        filesize < 20MB and
        all of them
}
rule apt_Reaper_extremevnc {
    meta:
        id = "c519de4f-1db5-4d4a-93b8-f1e7c0827af0"
        version = "1.0"
        malware = "ExtremeVNC"
        intrusion_set = "Reaper"
        description = "Detects ExtremeVNC implant (Reaper)"
        source = "SEKOIA.IO"
        creation_date = "2023-03-09"
        classification = "TLP:WHITE"
    strings:
        $ = "--myboundary--"
        $ = "COntent-Transfer-Encoding: 8bit"
        $ = "CLIP_REQ"
        $ = "SC_REQ"
        $ = "BROWSER_REQ"
        $ = "Unknown-PC"
    condition:
        uint16be(0) == 0x4d5a and
        filesize < 1MB and 
        4 of them
}
rule apt_Reaper_MFC42_Loader {
    meta:
        id = "23a3eaff-2813-48a2-91c6-c5bc1c0873ac"
        version = "1.0"
        intrusion_set = "Reaper"
        description = "Detects MFC42 loaders (Reaper)"
        source = "SEKOIA.IO"
        creation_date = "2023-03-09"
        classification = "TLP:WHITE"
    strings:
        $ = {50 68 01 00 00 00 B8 4D 3C 2B 1A FF D0}
    condition:
        uint16be(0) == 0x4d5a
        and filesize < 2MB 
        and all of them
}
